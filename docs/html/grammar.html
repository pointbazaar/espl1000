<!doctype html>
<html lang="en">
<head>
  <meta charset="utf-8">

  <title>Grammar</title>
  <meta name="description" content="Grammar">
  <meta name="author" content="ESPL1000">

  <link rel="icon" href="../img/favicon.ico">
  <link href="https://cdn.jsdelivr.net/npm/bootstrap@5.0.0-beta1/dist/css/bootstrap.min.css" rel="stylesheet" integrity="sha384-giJF6kkoqNQ00vy+HMDP7azOuL0xtbfIcaT9wjKHr8RbDVddVHyTfAAsrekwKmP1" crossorigin="anonymous">
  <link rel="stylesheet" href="../css/style.css">

</head>
<body>
<div class="container-xl">
	
	<div class="row nav-bg">
		<div class="col">
			<a href="../index.html"><h1>Home</h1></a>
		</div>
		<div class="col-2">
			<a href="subroutines.html"><h1>next</h1></a>
		</div>
	</div>
	

	<h1>Grammar</h1>

	<h2>Helpers</h2>

<pre>
arrow         ::= "->" | "~>"
lowercase     ::= 'a'|'b'|'c'|'d'|'e'|'f'|'g'|'h'|'i'|'j'|'k'|'l'|'m'|'n'|'o'|'p'|'q'|'r'|'s'|'t'|'u'|'v'|'w'|'x'|'y'|'z'
uppercase     ::= 'A'|'B'|'C'|'D'|'E'|'F'|'G'|'H'|'I'|'J'|'K'|'L'|'M'|'N'|'O'|'P'|'Q'|'R'|'S'|'T'|'U'|'V'|'W'|'X'|'Y'|'Z'
digit         ::= '0'|'1'|'2'|'3'|'4'|'5'|'6'|'7'|'8'|'9'
alphanumeric  ::= lowercase | uppercase | digit
Op            ::= "+" | "-" | "*" | "/" | "%" | "<<" | ">>" | "|" | "&" | "^" | "||" | "&&"
Id            ::= lowercase (lowercase | '_' | digit)*
</pre>

	<h2>Toplevel</h2>
<pre>
AST           ::= Namespace+

Namespace     ::= ExternC* StructDecl* Method*

StructDecl    ::= "struct" SimpleType '{' StructMember* '}'

StructMember  ::= Type Id ';'

Method        ::= MethodDecl StmtBlock

MethodDecl    ::= "fn" Id '(' DeclArg* ')' arrow Type

DeclArg       ::= Type Id? 

ExternC       ::= "externc" (MethodDecl | StructDecl) StringConst 
</pre>


	<h2>Types</h2>
<pre>
Type          ::= BasicType | TypeParam | ArrayType
	
BasicType     ::= SimpleType | '(' SubrType ')'

SimpleType    ::= PrimitiveType | StructType 

PrimitiveType ::= "int" | "float" | "uint" | "char" | "bool" | "uint8" | "int8" | "uint16" | "int16" | "uint32" | "int32" | "uint64" | "int64"
	
SubrType      ::= '(' Type* ')' arrow Type

StructType    ::= uppercase alphanumeric* ('<' Type (',' Type)* '>')? | "#"

TypeParam     ::= "?T" digit

ArrayType     ::= '[' Type ']'
</pre>

	<h2>Statements</h2>
<pre>
StmtBlock     ::= '{' Stmt* '}'

Stmt          ::= ( Call ';' )
		| whileStmt | ifStmt | forStmt | switchStmt  
		| returnStmt | assignStmt
		| "break" ';' | "continue" ';' 

Call         ::= Variable '(' (Expr (, Expr)*)?  ')'

whileStmt    ::= "while" Expr        StmtBlock
forStmt      ::= "for" Id "in" Range StmtBlock

ifStmt       ::= "if" Expr StmtBlock ( "else" StmtBlock )?
				
returnStmt   ::= "return" Expr? ';'

assignStmt   ::= Type? Variable '=' Expr ';'

switchStmt   ::= "switch" Expr '{' caseStmt* '}'

caseStmt     ::= "case" ConstValue StmtBlock?
</pre>
	
	<h2>Expressions</h2>
<pre>
Expr         ::= UnOpTerm (Op UnOpTerm)*

UnOpTerm     ::= Op Term 

Term         ::= ConstValue | StringConst | Call | '(' Expr ')' | Variable | Lambda

ConstValue   ::= BoolConst | IntConst | BinConst | HexConst | CharConst | FloatConst

Range        ::= Expr ".." Expr

Lambda       ::= '(' Id (',' Id)* ')' arrow Expr

ArrayConstant ::= '[' Expr (',' Expr)* ']'

Variable      ::= SimpleVar ('.' Variable)?

SimpleVar     ::= Id ('[' Expr ']')*
</pre>
	
</div>

</body>
</html>
